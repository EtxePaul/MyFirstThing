FUNCTION FC1828 : VOID
    TITLE = 'Pyrocontrol LH22'
    VERSION : '1.0'
    KNOW_HOW_PROTECT
    NAME : 'Talens'
    AUTHOR : 'Talens'   
    
VAR
POWER                     : WORD;
POWER_RATIO, SPEED_RATIO  : REAL;
PART_TYPE                 : INT;
CURRENT_JOURNAL           : INT;
CURRENT_BORE              : INT;
END_VAR

BEGIN 

CURRENT_JOURNAL := DINT_TO_INT(DB2501.LH22_SENT_DATA.CURRENT_JOURNAL);
CURRENT_BORE    := DINT_TO_INT(DB2501.LH22_SENT_DATA.CURRENT_BORE);
PART_TYPE       := BYTE_TO_INT(DB1965.Part_Type_Low);
POWER_RATIO     := DINT_TO_REAL(DB2501.LH22_RECV_DATA.POWER_RATIO)/1000;
SPEED_RATIO     := DINT_TO_REAL(DB2501.LH22_RECV_DATA.SPEED_RATIO);

DB1810.PYROCONTROL_LH22.PID.HMI_MAX_POWER := 5000.0;

//-----------------------Transfering the values depending on Bore and Journal---------------------------//
DB701.LH22.Power_Init_Current := DB701.Parameters.Bore[CURRENT_BORE].Power_Init[CURRENT_JOURNAL];
DB701.LH22.Timer_Init_Current := DB701.Parameters.Bore[CURRENT_BORE].Timer_Init[CURRENT_JOURNAL];
DB701.LH22.Power_2_Current    := DB701.Parameters.Bore[CURRENT_BORE].Power_2[CURRENT_JOURNAL];
DB701.LH22.Timer_2_Current    := DB701.Parameters.Bore[CURRENT_BORE].Timer_2[CURRENT_JOURNAL];

IF DB701.LH22.Harden_step_1 THEN
  POWER :=  INT_TO_WORD(REAL_TO_INT(DB701.LH22.Power_Init_Current));
ELSIF DB701.LH22.Harden_step_2  THEN
  POWER :=  INT_TO_WORD(REAL_TO_INT(DB701.LH22.Power_2_Current));
END_IF;
//------------------------------------------------------------------------------------------------------//  

//----------------------------------------- PYROMETER --------------------------------------------------//
DB1810.PYROCONTROL_LH22.PYROMETER_ACTUAL_TEMP := DB1810.PYROCONTROL_LH22.PYROMETER_GAIN * DB1810.PYROCONTROL_LH22.I_PYROMETER + DB1810.PYROCONTROL_LH22.PYROMETER_OFFSET;

IF NOT MCHN_SQ_SQ_RUNNING THEN
    DB1810.PYROCONTROL_LH22.OPTIMAL_OFFSET :=  500 - DB1810.PYROCONTROL_LH22.PYROMETER_GAIN * DB1810.PYROCONTROL_LH22.I_PYROMETER;  
ELSE
    DB1810.PYROCONTROL_LH22.OPTIMAL_OFFSET := 0;    
END_IF;
//------------------------------------------------------------------------------------------------------//  

//---------------------------------------- AXIS A SPEED ------------------------------------------------//
IF (SPEED_RATIO > 0 AND SPEED_RATIO <= 2000) THEN
  DB1810.PYROCONTROL_LH22.SPEED_RATIO    := (SPEED_RATIO/10);
ELSE
  DB1810.PYROCONTROL_LH22.SPEED_RATIO    :=100;
END_IF;    
//------------------------------------------------------------------------------------------------------//

//-----------------------------------------POWER CORRECTION---------------------------------------------//
DB1810.PYROCONTROL_LH22.CORR_POWER_H:=DB1268.POWER_CORR_LH22.POWER_H[PART_TYPE,CURRENT_BORE,CURRENT_JOURNAL];

IF AZ_LH22_SQ_SEQ_RUNNING AND DB1860.SENSOR_22.EmissionOn THEN
  POWER := DINT_TO_WORD(DB1860.SENSOR_22.CommandPower);
END_IF; 

IF DB1211.M_CODES_UNIT_01L.M46 OR AZ_LH22_SQ_SEQ_RUNNING THEN
    DB1810.PYROCONTROL_LH22.CORR_OUT_POWER := INT_TO_REAL(WORD_TO_INT(POWER));
ELSE
    DB1810.PYROCONTROL_LH22.CORR_OUT_POWER := 0;
END_IF;

// Laser command power check: M100.0 -> UA_RTR_AX_SPD_CHCK
// Command power greater than 10% of (Laser max power): UA_RTR_AX_SPD_CHCK = TRUE

IF DB1810.PYROCONTROL_LH22.CORR_OUT_POWER >= DB1810.PYROCONTROL_LH22.PID.HMI_MAX_POWER*0.1 THEN
  laserCmdPowerCheck_LH22:=TRUE;
END_IF;

IF (DB1810.PYROCONTROL_LH22.CORR_OUT_POWER < DB1810.PYROCONTROL_LH22.PID.HMI_MAX_POWER*0.1) OR (DB1211.M_CODES_UNIT_01L.M11=TRUE) OR (CCL_B_SQ_RUNNING=FALSE AND AZ_LH22_SQ_SEQ_RUNNING=FALSE) THEN
  laserCmdPowerCheck_LH22:=FALSE;
END_IF;

// Rotary axis speed check: M100.1 -> UA_RTR_AX_SPD_CHCK
// Speed = 0 : UA_RTR_AX_SPD_CHCK = TRUE
 
IF DB2501.LH22_SENT_DATA.PROCESS_SPEED = 0 THEN
  rotaryAxisSpeedCheck_H22:=TRUE;
ELSIF DB2501.LH22_SENT_DATA.PROCESS_SPEED > 0 OR CCL_B_SQ_RUNNING=FALSE THEN
  rotaryAxisSpeedCheck_H22:=FALSE;
END_IF;
//------------------------------------------------------------------------------------------------------//

IF (NOT LSR_22_ACT_LASER_ACT_ON) THEN
  POWER:=0;
END_IF;

DB1178.laser_22.Outputs.Power := WORD_TO_INT(ROL(IN:=POWER , N:=8));

END_FUNCTION